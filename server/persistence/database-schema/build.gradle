buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.flywaydb:flyway-database-postgresql:11.17.2")
    }
}

plugins {
    id "com.avast.gradle.docker-compose" version "0.17.20"
    id 'nu.studer.jooq' version '10.1.1'
    id "org.flywaydb.flyway" version "11.17.2"
    id "java-library"
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.flywaydb:flyway-database-postgresql:11.17.2"
    api "com.zaxxer:HikariCP:7.0.2"
    api 'org.jooq:jooq:3.20.9'

    // Needs to be 'api' scope so the Flyway plugin can see it
    api "org.postgresql:postgresql:$postgresDriverVersion"

    jooqGenerator "org.postgresql:postgresql:$postgresDriverVersion"
}

flyway {
    url = 'jdbc:postgresql://localhost:5433/projektordb'
    user = 'testuser'
    password = 'testpass'
}

jooq {
    version = '3.20.2'

    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5433/projektordb'
                    user = 'testuser'
                    password = 'testpass'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    strategy {
                        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                    }
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        excludes = "flyway_schema_history | shedlock"
                        inputSchema = "public"
                    }
                    generate {
                        relations = true
                        deprecated = false
                        records = true
                        fluentSetters = true
                        pojos = true
                        pojosEqualsAndHashCode = true
                        daos = true
                    }
                    target {
                        packageName = 'projektor.database.generated'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}

dockerCompose { // https://github.com/avast/gradle-docker-compose-plugin?tab=readme-ov-file#usage
    captureContainersOutput = true
    waitForTcpPorts = true
}

generateJooq.inputs.dir("${projectDir}/src/main/resources/db/migration")
generateJooq.outputs.cacheIf { true }

//flywayMigrate.dependsOn composeUp
generateJooq.dependsOn flywayMigrate
//generateJooq.finalizedBy composeDownForced
