plugins {
    id 'java-gradle-plugin'
    id 'java-test-fixtures'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "1.3.0"
    id 'com.github.psxpaul.execfork'
    id 'groovy'
}

sourceSets {
    functionalTest {
        groovy.srcDir file('src/functionalTest/groovy')
        resources.srcDir file('src/functionalTest/resources')
        compileClasspath += sourceSets.main.output + sourceSets.testFixtures.output
        runtimeClasspath += output + compileClasspath
    }
}

gradlePlugin {
    website = 'https://projektor.dev/docs/gradle-plugin/'
    vcsUrl = 'https://github.com/craigatk/projektor'
    plugins {
        projektorPlugin {
            id = 'dev.projektor.publish'
            implementationClass = 'projektor.plugin.ProjektorPublishPlugin'
            displayName = 'Projektor publishing plugin'
            description = 'Automatically publish test reports to a Projektor report server'
            tags.addAll(['testing', 'report'])
        }
    }
    testSourceSets sourceSets.test, sourceSets.functionalTest
}

repositories {
    mavenCentral()
}

group = "dev.projektor"
version = "9.0.0"

dependencies {
    implementation "com.squareup.okhttp3:okhttp:${ok_http3_version}"

    // Needs to stay at Jackson 2.15.x until Projektor Gradle plugin requires Gradle 8.4 or above
    // Ref https://stackoverflow.com/questions/77225378/gradle-clean-fails-with-gradleexception-failed-to-create-jar-file
    implementation "com.fasterxml.jackson.core:jackson-databind:2.15.3"

    implementation "io.github.resilience4j:resilience4j-retry:2.3.0"

    implementation "org.eclipse.jgit:org.eclipse.jgit:7.1.0.202411261347-r"

    testImplementation("org.spockframework:spock-core:$spockVersion") {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation "org.spockframework:spock-junit4:$spockVersion"
    testImplementation "net.bytebuddy:byte-buddy:1.16.1"
    testImplementation 'org.objenesis:objenesis:3.4'

    testImplementation "com.github.tomakehurst:wiremock-jre8:$wiremockVersion"

    testImplementation(project(":server:parsing:coverage-parser"))
    testImplementation(project(":server:parsing:grouped-results-parser"))
    testImplementation(project(":server:test:test-fixtures"))

    testFixturesImplementation "org.spockframework:spock-core:$spockVersion"
    testFixturesImplementation "org.spockframework:spock-junit4:$spockVersion"

    functionalTestImplementation "org.spockframework:spock-core:$spockVersion"
    functionalTestImplementation "org.spockframework:spock-junit4:$spockVersion"
    functionalTestImplementation gradleTestKit()
    functionalTestImplementation("com.squareup.okhttp3:okhttp:${ok_http3_version}")
    functionalTestImplementation("com.squareup.okhttp3:logging-interceptor:${ok_http3_version}")

    functionalTestImplementation("org.apache.commons:commons-lang3:3.17.0")
    functionalTestImplementation(project(":server:server-client"))
}

test {
    useJUnitPlatform()

    maxParallelForks = Math.floorDiv(Runtime.runtime.availableProcessors(), 2) + 1
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath

    useJUnitPlatform()
}

check.dependsOn functionalTest

// https://github.com/psxpaul/gradle-execfork-plugin
task startServerDaemon(type: com.github.psxpaul.task.JavaExecFork) {
    dependsOn ':server:server-app:shadowJar',
            'compileFunctionalTestGroovy',
            'compileGroovy',
            'compileTestGroovy',
            'compileTestFixturesGroovy',
            'compileFunctionalTestJava',
            'javadocJar',
            'pluginUnderTestMetadata',
            'sourcesJar',
            'processFunctionalTestResources',
            'processTestResources',
            'validatePlugins'
    mustRunAfter 'test'
    classpath = tasks.getByPath(':server:server-app:shadowJar').outputs.files
    main = 'io.ktor.server.netty.EngineMain'
    stopAfter = functionalTest
    waitForOutput = 'Application started'
    timeout = 120
    environment = [
            'PORT': '8092',
            "ATTACHMENT_URL": "http://localhost:9000",
            "ATTACHMENT_BUCKET_NAME": "functionaltest",
            "ATTACHMENT_AUTO_CREATE_BUCKET": "true",
            "ATTACHMENT_ACCESS_KEY": "minio_access_key",
            "ATTACHMENT_SECRET_KEY": "minio_secret_key"
    ]
}
functionalTest.dependsOn startServerDaemon
