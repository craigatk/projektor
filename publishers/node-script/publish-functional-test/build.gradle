import projektor.plugin.ProjektorManualPublishTask

plugins {
    id 'com.github.psxpaul.execfork'
    id "com.github.node-gradle.node" version "3.5.1"
}

node {
    download = false
}

evaluationDependsOn(':server:server-app')

task projektorPublish(type: ProjektorManualPublishTask) {
    serverUrl = "https://projektorlive.herokuapp.com"
    publishToken = System.getenv("PROJEKTOR_TOKEN") ?: project.findProperty('projektor_publish_token') ?: null
    additionalResultsDirs = ["testResults"]
    projectName = "node-script-functional-test"
}

task projektorPublishWithToken(type: ProjektorManualPublishTask) {
    serverUrl = "https://projektorlive.herokuapp.com"
    publishToken = System.getenv("PROJEKTOR_TOKEN") ?: project.findProperty('projektor_publish_token') ?: null
    additionalResultsDirs = ["testResults"]
    projectName = "node-script-functional-test-with-token"
}

task functionalTest(type: YarnTask) {
    dependsOn 'startServerDaemon'
    args = ['test']
    finalizedBy(projektorPublish)
}

task functionalTestWithToken(type: YarnTask) {
    dependsOn 'startServerDaemonWithToken'
    args = ['test-with-token']
    finalizedBy(projektorPublishWithToken)
}

task allFunctionalTests(dependsOn: [functionalTest, functionalTestWithToken])

// https://github.com/psxpaul/gradle-execfork-plugin
task startServerDaemon(type: com.github.psxpaul.task.JavaExecFork) {
    dependsOn ':server:server-app:shadowJar','yarnSetup'
    classpath = tasks.getByPath(':server:server-app:shadowJar').outputs.files
    main = 'io.ktor.server.netty.EngineMain'
    stopAfter = functionalTest
    waitForPort = 8082
    waitForOutput = 'Application started'
    timeout = 120
    environment = [
            'PORT': '8082',
            "ATTACHMENT_URL": "http://localhost:9000",
            "ATTACHMENT_BUCKET_NAME": "functionaltest",
            "ATTACHMENT_AUTO_CREATE_BUCKET": "true",
            "ATTACHMENT_ACCESS_KEY": "minio_access_key",
            "ATTACHMENT_SECRET_KEY": "minio_secret_key"
    ]
}

// https://github.com/psxpaul/gradle-execfork-plugin
task startServerDaemonWithToken(type: com.github.psxpaul.task.JavaExecFork) {
    dependsOn ':server:server-app:shadowJar','yarnSetup'
    classpath = tasks.getByPath(':server:server-app:shadowJar').outputs.files
    main = 'io.ktor.server.netty.EngineMain'
    stopAfter = functionalTest
    waitForPort = 8083
    waitForOutput = 'Application started'
    timeout = 120
    environment = [
            'PORT': '8083',
            'PUBLISH_TOKEN': '123456',
            "ATTACHMENT_URL": "http://localhost:9000",
            "ATTACHMENT_BUCKET_NAME": "functionaltest",
            "ATTACHMENT_AUTO_CREATE_BUCKET": "true",
            "ATTACHMENT_ACCESS_KEY": "minio_access_key",
            "ATTACHMENT_SECRET_KEY": "minio_secret_key"
    ]
}
