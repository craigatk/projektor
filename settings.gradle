plugins {
    id "com.atkinsondev.object-store-cache" version "1.1.2"
}

include 'functional-test',
        'publishers:gradle-plugin',
        'publishers:node-script:publish-functional-test',
        'server:parsing:coverage-parser',
        'server:parsing:grouped-results-parser',
        'server:parsing:jacoco-xml-parser',
        'server:parsing:jest-xml-parser',
        'server:parsing:junit-results-parser',
        'server:parsing:test-results-parser',
        'server:persistence:database-schema',
        'server:persistence:object-store',
        'server:server-app',
        'server:server-api',
        'server:server-client',
        'server:test:coverage-fixtures',
        'server:test:server-example-loader',
        'server:test:test-fixtures',
        'ui'

boolean isCI = Boolean.valueOf(System.getenv("GITHUB_ACTIONS"))
String cacheAccessKey = System.getenv("CACHE_ACCESS_KEY") ?: properties['cache_access_key']
String cacheSecretKey = System.getenv("CACHE_SECRET_KEY") ?: properties['cache_secret_key']

buildCache {
    local {
        enabled = !isCI
    }
    remote(com.atkinsondev.cache.ObjectStoreBuildCache) {
        endpoint = 'sfo2.digitaloceanspaces.com'
        accessKey = cacheAccessKey
        secretKey = cacheSecretKey
        bucket = 'projektorcache'
        autoCreateBucket = true
        expirationInDays = 10
        push = isCI
    }
}