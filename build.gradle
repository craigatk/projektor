buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.2.1"
    }
}

plugins {
    id "dev.projektor.publish" version "8.0.2"
    id 'com.github.psxpaul.execfork' version '0.2.0' apply false
}

projektor {
    serverUrl = "https://projektorlive.herokuapp.com"
    publishToken = System.getenv("PROJEKTOR_TOKEN") ?: project.findProperty('projektor_publish_token') ?: null
    codeQualityReports = [fileTree(dir: ".", include: "**/build/reports/ktlint/**/*.txt")]
}

allprojects {
    buildscript {
        repositories {
            maven { url "https://plugins.gradle.org/m2/" }
            mavenCentral()
        }
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat = 'full'
        }

        reports {
            junitXml {
                outputPerTestCase = true
            }
        }
    }

    tasks.withType(JacocoReport) {
        dependsOn(test)
    }

    plugins.withType(JacocoPlugin) {
        jacoco {
            toolVersion = "0.8.7"
        }
    }
}
