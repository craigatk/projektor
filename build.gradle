buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.5.1"
    }
}

plugins {
    id "dev.projektor.publish" version "8.7.0"
    id "com.atkinsondev.opentelemetry-build" version "1.10.0"
    id 'com.github.psxpaul.execfork' version '0.2.2' apply false
}

projektor {
    serverUrl = "https://projektorlive.herokuapp.com"
    publishToken = System.getenv("PROJEKTOR_TOKEN") ?: project.findProperty('projektor_publish_token') ?: null
    codeQualityReports = [fileTree(dir: ".", include: "**/build/reports/ktlint/**/*.txt")]
}

openTelemetryBuild {
    endpoint = "https://api.honeycomb.io"
    headers = [
            "x-honeycomb-team": System.getenv("HONEYCOMB_API_KEY") ?: project.findProperty('honeycomb_api_key') ?: null,
            "x-honeycomb-dataset": "otel-gradle"
    ]
}

allprojects {
    buildscript {
        repositories {
            maven { url "https://plugins.gradle.org/m2/" }
            mavenCentral()
        }
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat = 'full'
        }

        reports {
            junitXml {
                outputPerTestCase = true
            }
        }
    }

    tasks.withType(JacocoReport) {
        dependsOn(test)
    }

    plugins.withType(JacocoPlugin) {
        jacoco {
            toolVersion = "0.8.12"
        }
    }
}
